#
#	Phase 0 of the Orthologous matrix
#	Basic definitions and procedures
#					Gaston H. Gonnet (July 2004)
Set(quiet):
SetRandSeed();
Set(gc=3e6):

if not assigned(YeastAshbyaCounts) then
  ReadProgram('/home/darwin/v2/source/bio-recipes/Orthologues/lib/darwinit') fi;
if not assigned(LoadedGenome) then LoadedGenome := table() fi:

if not assigned(typ) then typ := 'SEQ' fi:
if not member(typ,{'SEQ','DNA'}) then
    lprint(typ,'type of database, typ, is incorrect');  quit fi;

if GenomeFileName[name1] = unassigned then
	lprint( name1, 'is not a valid genome name');  quit fi;
if LoadedGenome[name1] = unassigned then
     LoadedGenome[name1] := db1 := ReadDb( GenomeFileName[name1] )
else db1 := LoadedGenome[name1] fi:

if GenomeFileName[name2] = unassigned then
	lprint( name2, 'is not a valid genome name');  quit fi;
if LoadedGenome[name2] = unassigned then
     LoadedGenome[name2] := db2 := ReadDb( GenomeFileName[name2] )
else db2 := LoadedGenome[name2] fi:

n1 := db1[TotEntries]:
n2 := db2[TotEntries]:
if DNR <> true and (n1 > n2 or n1=n2 and name1 > name2) then
    t := db1;  db1 := db2;  db2 := t;
    t := name1;  name1 := name2;  name2 := t;
    t := n1;  n1 := n2;  n2 := t;
    fi:

# Clean up sequences for comparison
Seqs1 := CreateArray(1..n1):
Seqs2 := CreateArray(1..n2):
DB := db1:
for i1 to n1 do Seqs1[i1] := SearchTag(typ,Entry(i1)) od:
DB := db2:
for i2 to n2 do Seqs2[i2] := SearchTag(typ,Entry(i2)) od:


# Value 224 taken from many runs of Phase6.drw and the summarizing
# program AnalyzeBestDM.drw.
CreateDayMatrices();  DMSprot := DMS:  DM := DayMatrix(224):
if typ='DNA' then
     CreateDayMatrices( YeastAshbyaCounts );  DM := DayMatrix(68) fi;

